plugins {
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '2.5.0'
    id "io.freefair.lombok" version "5.2.1"
}

group = 'ru.fondorg'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.test.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    //implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    runtimeOnly 'mysql:mysql-connector-java:8.0.22'
    implementation 'org.keycloak:keycloak-spring-boot-starter'
    implementation 'io.springfox:springfox-swagger2:3.0.0'
    implementation 'io.springfox:springfox-swagger-ui:3.0.0'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'io.springfox:springfox-bean-validators:3.0.0'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.modelmapper:modelmapper:2.3.9'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'com.h2database:h2'

    intTestImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    intTestImplementation 'org.springframework.security:spring-security-test'
    intTestRuntimeOnly 'com.h2database:h2'
}

test {
    useJUnitPlatform()
}

jib {
    from {
//		image 'gradle:jdk11'
        image 'openjdk:11.0-jre'

    }
    to {
        image 'fondorg/mytrack-srv'
    }
}

task integrationTest(type: Test) {
    group = 'verification'
    description = 'Run the integration tests'
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

//check.dependsOn(integrationTest)

dependencyManagement {
    imports {
        mavenBom 'org.keycloak.bom:keycloak-adapter-bom:11.0.2'
    }
}